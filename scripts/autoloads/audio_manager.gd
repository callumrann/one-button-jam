extends Node

@onready var bgm_player: AudioStreamPlayer = AudioStreamPlayer.new()

const SFX := {
	"click": preload("res://assets/audio/click.wav"),
	"damage": preload("res://assets/audio/damage.wav"),
	"level_win": preload("res://assets/audio/level_win.wav"), # too happy sounding
	"menu_move_1": preload("res://assets/audio/menu_move_1.wav"),
	"menu_move_2": preload("res://assets/audio/menu_move_2.wav"),
	"menu_select": preload("res://assets/audio/menu_select.wav"),
	"pause_in": preload("res://assets/audio/pause_in.wav"),
	"pause_out": preload("res://assets/audio/pause_out.wav"),
	"jump": preload("res://assets/audio/jump.wav"),
	"wall_jump": preload("res://assets/audio/wall_jump.wav"),
	"land": preload("res://assets/audio/land.wav"),
	"saw": preload("res://assets/audio/saw.wav"),
}

const MUSIC := {
	"menu": preload("res://assets/audio/music_2.wav"),
	"stage_1": preload("res://assets/audio/music_1.wav"),
	"stage_2": preload("res://assets/audio/music_3.wav"),
	"stage_3": preload("res://assets/audio/music_4.wav"),
}

func _ready() -> void:
	add_child(bgm_player)
	bgm_player.bus = "Music"
	play_music("menu", -10)

func update_button_sfx() -> void:
	var buttons: Array = get_tree().get_nodes_in_group("Button")
	for inst in buttons:
		if not inst.pressed.is_connected(_on_button_pressed):
			inst.pressed.connect(_on_button_pressed)
		if not inst.mouse_entered.is_connected(_on_button_hover):
			inst.mouse_entered.connect(_on_button_hover)

func _on_button_pressed() -> void:
	play_sfx("click", -10)

func _on_button_hover() -> void:
	play_sfx("menu_move_1", -15)

func play_music(track_name: String, volume: float = 0.0) -> void:
	if track_name == "stage_4":
		track_name = "stage_3"
		set_music_speed(1.5)
	else:
		set_music_speed(1)
	
	if not MUSIC.has(track_name):
		push_warning("Unknown music track: " + track_name)
		set_music_speed(1)
		return
	
	var stream = MUSIC[track_name]
	if bgm_player.stream == stream and bgm_player.playing:
		return
	bgm_player.stream = stream
	bgm_player.volume_db = volume
	bgm_player.play()

func stop_music() -> void:
	bgm_player.stop()

func play_sfx(sfx_name: String, volume: float = 0.0) -> void:
	if not SFX.has(sfx_name):
		push_warning("Unknown SFX: " + sfx_name)
		return
	var sfx_player := AudioStreamPlayer.new()
	sfx_player.stream = SFX[sfx_name]
	sfx_player.volume_db = volume
	sfx_player.bus = "SFX"
	add_child(sfx_player)
	sfx_player.play()
	sfx_player.finished.connect(sfx_player.queue_free)

func set_music_speed(speed_factor: float) -> void:
	bgm_player.pitch_scale = speed_factor
	var bus_index = AudioServer.get_bus_index("Music")
	var pitch_shift = AudioServer.get_bus_effect(bus_index, 0) as AudioEffectPitchShift
	pitch_shift.pitch_scale = 1.0 / speed_factor
